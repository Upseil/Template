// Game HTML

apply plugin: "gwt"
apply plugin: "war"

buildscript {
    dependencies {
        classpath "org.wisepersist:gwt-gradle-plugin:$gwtGradleTarget"
    }
}

dependencies {
    compile project(":core")
    compile project(":upseilgdx:gwt")
    
    compile "com.google.gwt:gwt-user:$gwtTarget"
    
    compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxTarget"
    compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxTarget:sources"
    compile "com.badlogicgames.gdx:gdx:$gdxTarget:sources"
    compile "com.badlogicgames.gdx:gdx-box2d:$gdxTarget:sources"
    compile "com.badlogicgames.gdx:gdx-box2d-gwt:$gdxTarget:sources"
    
    compile "net.onedaybeard.artemis:artemis-odb-gwt:$artemisTarget"
    compile "net.onedaybeard.artemis:artemis-odb-gwt:$artemisTarget:sources"
    compile "net.onedaybeard.artemis:artemis-odb:$artemisTarget:sources"
    
    compile "com.github.nmorel.gwtjackson:gwt-jackson:$jacksonGwtTarget"
}

task copyGameInit(type: Copy) {
    from file("../core/assets/game.init")
    into file("src/com/upseil/game/")
}

tasks.compileGwt.dependsOn(copyGameInit)
tasks.draftCompileGwt.dependsOn(copyGameInit)

task addSource {
	doLast {
		sourceSets.main.compileClasspath += files(project(':core').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':upseilgdx:gdx').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':upseilgdx:util').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':upseilgdx:artemis').sourceSets.main.allJava.srcDirs)
        sourceSets.main.compileClasspath += files(project(':upseilgdx:box2d').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':upseilgdx:gwt').sourceSets.main.allJava.srcDirs)
	}
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)

gwt {
    gwtVersion="$gwtTarget" // Should match the gwt version used for building the gwt backend
    maxHeapSize="1G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
    minHeapSize="1G"

    src = files(file("src/")) // Needs to be in front of "modules" below.
    modules 'com.upseil.game.html'
    devModules 'com.upseil.game.htmlSuperdev'
    project.webAppDirName = 'webapp'

    compiler {
        strict = true;
        disableCastChecking = true;
    }
}

import org.wisepersist.gradle.plugins.gwt.GwtSuperDev

task startHttpServer () {
    dependsOn draftCompileGwt

    doLast {
    	String output = project.buildDir.path + "/gwt/draftOut"
    
        copy {
            from "webapp"
            into output
        }
        copy {
            from "war"
            into output
        }

        HttpFileServer server = new SimpleHttpFileServerFactory().start(new File(output), 8080)

        println "Server started in directory " + server.getContentRoot() + ", http://localhost:" + server.getPort()
    }
}

task superDev (type: GwtSuperDev) {
	description 'Starts a server and runs the GWT super dev mode'
    dependsOn startHttpServer
    doFirst {
        gwt.modules = gwt.devModules
    }
}

task dist(dependsOn: [clean, compileGwt]) {
    doLast {
        file("build/dist").mkdirs()
        copy {
            from "build/gwt/out"
            into "build/dist"
        }
        copy {
            from "webapp"
            into "build/dist"
            }
        copy {
            from "war"
            into "build/dist"
        }
    }
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)

eclipse.project {
    name = workingTitle + "-Html"
}
